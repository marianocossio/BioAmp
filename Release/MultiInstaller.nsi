; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Multi_Installer"
!define PRODUCT_VERSION "1.0"
!define PRODUCT_PUBLISHER "UNER"

; MUI 1.67 compatible ------
!include "MUI2.nsh"
!include "LogicLib.nsh"
!include "nsDialogs.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "..\bitmap.ico"

Var Dialog
Var BioAmp_CheckBox
Var BioAmp_CheckBox_State
Var BrainBay_CheckBox
Var BrainBay_CheckBox_State
Var Label

Page custom nsDialogsPage checkInstall
!define MUI_PAGE_HEADER_SUBTEXT "Please wait while ${PRODUCT_NAME} ${PRODUCT_VERSION} installs the required software"
!insertmacro MUI_PAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

Function nsDialogsPage

  nsDialogs::Create 1018
  Pop $Dialog

  ${If} $Dialog == error
    Abort
  ${EndIf}
  
  ${NSD_CreateLabel} 0u 0u 100% 10u "Select Programs to install:"
  Pop $Label

  ${NSD_CreateCheckBox} 20u 30u 100% 30 "Bio&Amp 1.0"
  Pop $BioAmp_CheckBox

  ${NSD_CreateCheckBox} 20u 50u 100% 30 "&BrainBay 2.0"
  Pop $BrainBay_CheckBox

  ${NSD_Check} $BioAmp_CheckBox
  ${NSD_Check} $BrainBay_CheckBox

  nsDialogs::Show

FunctionEnd

Function checkInstall

  ${NSD_GetState} $BioAmp_CheckBox $BioAmp_CheckBox_State
  ${NSD_GetState} $BrainBay_CheckBox $BrainBay_CheckBox_State

FunctionEnd

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "MultiInstaller_1.0.exe"

Section "MainSection" SEC01

  SetOutPath "$TEMP"
  SetOverwrite ifnewer
  ${If} $BioAmp_CheckBox_State == ${BST_CHECKED}
    File "BioAmp_1.0_Setup.exe"
    ExecWait "BioAmp_1.0_Setup.exe"
    Delete "BioAmp_1.0_Setup.exe"
  ${EndIf}

  ${If} $BrainBay_CheckBox_State == ${BST_CHECKED}
    File "..\..\Software\ad_hoc\Setup_BrainBay_v2.0.exe"
    ExecWait "Setup_BrainBay_v2.0.exe"
    Delete "Setup_BrainBay_v2.0.exe"
  ${EndIf}

SectionEnd